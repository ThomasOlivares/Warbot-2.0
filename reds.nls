;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; le code de l'equipe verte basique
;; préfixe : durandil
;;
;; item 0 mem0 et item 1 mem0 : coordonnees d'une cible
;; mem5 : indique si le robot a ou non une cible sélectionnée
;; mem4 : indique si un harvester est en mode retour à la base
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to durandil-ask-for-energy [ b n ]
  ask b [ if (energy > 1000) [ give-energy myself n ]]
end

to-report durandil-number-agent-in-range
  report count perceive-robots friend
end

to durandil-plant-seeds
  ;; plante une graine
  plant-seeds color max-seeds
  ;; détermine la base la plus proche
  let b mem1
  ;; si le robot est arrivé à la base
  ifelse (distance b <= 2)
    [ 
      ;; dépose sa nourriture
      give-food b carrying-food?
      ;; demande de l'énergie si il reste moins de 1000
      if (energy < 1000) [ durandil-ask-for-energy b 300 ]
      set mem4 0
      ;; fait demi-tour
      rt 180
    ]
  ;; sinon
    [
      ;; s'oriente vers la base avec un peu d'aléatoire
      set heading towards b - 60 + random 120
      ;; turtle will orbit left or right, depending on its id
      if ( who mod 7 = 1 or who mod 7 = 2 ) [ lt 30 ]
      if ( who mod 7 = 4 or who mod 7 = 5 ) [ rt 30 ]  
      
      ;; fait un demi tour si il y a du monde devant
      ifelse (free-ahead? speed = nobody)  [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
    ]
  
end

to durandil-harvester-go-back-to-base
   ;; détermine la base a atteindre
  let b mem1
  ;; si le robot est arrivé à la base
  ifelse (distance b <= 2)
    [ 
      ;; dépose sa nourriture
      give-food b carrying-food?
      ;; demande de l'énergie si il reste moins de 1000
      if (energy < 1000) [ durandil-ask-for-energy b 300 ]
      ;; demande de l'énergie pour créer 5 missiles
      
      set mem4 0
      ;; fait demi-tour
      rt 180
    ]
  ;; sinon
    [
      ;; s'oriente vers la base avec un peu d'aléatoire
      set heading towards b - 20 + random 40
      ;; fait un demi tour si il y a du monde devant
      ifelse (free-ahead? speed = nobody)  [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
    ]
end

;;
;; rentre à la base si le robot transporte plus de 1000 unités de nourriture ou si son énergie est inférieure à 500 ou si c'est un rocket-launcher qui n'a plus de missiles
;;
to durandil-go-back-to-base-without-food
  ;; détermine la base la plus proche
  let b min-one-of my-bases [ distance myself ]
  ; there is still one base
  if (b != nobody)
  [
    ;; si le robot est arrivé à la base
    ifelse (distance b <= 2)
    [ 
      ;; demande de l'énergie si il reste moins de 1000
      if (energy < 1000) [ durandil-ask-for-energy b 300 ]
      ;; demande de l'énergie pour créer 5 missiles
      if (Breed = RocketLaunchers) [
        durandil-ask-for-energy b 500
        if (nb-missiles = 0)
        [
          durandil-ask-for-energy b 50
          new-missile 50
        ]
        if (nb-fafs = 0)
        [
          durandil-ask-for-energy b 250
          new-faf 5
        ]
      ]
      if (mem4 = 2) [durandil-ask-for-energy b 1000]
      if (mem4 != 2)[ set mem4 0 ]
      ;; fait demi-tour
      rt 180
    ]
    ;; sinon
    [
      ;; s'oriente vers la base avec un peu d'aléatoire
      set heading towards b - 20 + random 40
      durandil-safe-move
    ]
  ]
end

to durandil-go-and-eat
  if ((breed = RocketLaunchers and (mem5 = 0)) or (breed = Explorers)) [
    random-move
  ]
end

to durandil-harvesters-go-and-eat
  let b min-one-of perceive-food [ distance myself ]
  ifelse (b != nobody) [ 
    ifelse (distance b) <= 2
      [ take b ]
      [ set heading towards b - 20 + random 40
      ifelse (free-ahead? speed = nobody) [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
      ]
  ]
  [
    ifelse (mem5 = 1)
    [ 
      ifelse (distancexy item 0 mem0 item 1 mem0 > 1)
      [
        set heading towardsxy item 0 mem0 item 1 mem0
        ifelse (free-ahead? speed = nobody) [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
      ]
      [ set mem5 0 ]
    ]
    [ random-move ]
  ]
end

;;
;; la mémoire mem5 est à
;; - 0 si le robot n'a pas de cible sélectionnée
;; - 1 si le robot a une cible sélectionnée
;;
to-report durandil-no-target?
  report mem5 <= 0
end

to durandil-set-target-xy [ x y ]
  set mem0 replace-item 0 mem0 x 
  set mem0 replace-item 1 mem0 y 
  set mem5 1
end

to durandil-set-target-t [ t ]
  set mem0 t 
  set mem5 2
end

to durandil-set-food-target [ x y ]
  if (durandil-no-target?) 
  [ 
    set mem0 replace-item 0 mem0 x 
    set mem0 replace-item 1 mem0 y 
    set mem5 1 
  ] 
end

to durandil-call-rocket-launcher-xy [ x y ]
  let rl min-one-of perceive-specific-robots color RocketLaunchers [ distancexy x y ]
  if (rl != nobody and [mem4] of rl != 2) [ ask rl [ durandil-set-target-xy x y ]]
end

to durandil-call-rocket-launcher-t [ t ]
  let rl min-one-of perceive-specific-robots color RocketLaunchers [ distancexy [xcor] of t [ycor] of t ]
  if (rl != nobody and [mem4] of rl != 2) [ ask rl [ durandil-set-target-t t ]]
end

to durandil-call-explorer [ x y ]
  let ex one-of perceive-specific-robots color Explorers
  if (ex != nobody) [
    ask ex [ durandil-set-target-xy x y ]
  ]
end

to durandil-call-harvester [ x y ]
  let h min-one-of perceive-specific-robots color harvesters [ distancexy x y ]
  if (h != nobody) [ ask h [ durandil-set-food-target x y ]]
end

;;
;; essaye de sélectionner une cible à viser
;;
to durandil-select-target
  ;; si le robot n'a pas une base adverse comme cible
  if (durandil-no-target?)
  [
    ;; il essaye de percevoir un robot ennemi (le plus proche de lui)
    let h min-one-of perceive-robots2 ennemy1 ennemy2 [ distance myself ]
    ;; si il en a vu un, il mémorise ses coordonnées et vérouille la cible
    ifelse ( h != nobody ) [
      set mem0  h
      set mem5 2
    ]
    ;; sinon, il se dévérouille
    [ set mem5 0 ]
  ]
end

to-report durandil-dodge
  without-interruption [
    ifelse (mem4 = 1 or Breed != RocketLaunchers or mem0 = nobody)[
    ]
    [
      if ([Breed] of mem0 = RocketLaunchers)
      [
        set heading towards mem0
        rt 90
        forward-move speed
      ]
    ]
  ]
  report 0
end

to durandil-shoot 
  if (mem0 != nobody)[
    if (not durandil-no-target?)
    [
      ifelse (mem5 = 1)
      [ 
        launch-rocket towardsxy item 0 mem0 item 1 mem0 
        set mem5 0 
      ]
      [ 
        ifelse ((nb-fafs > 0) and (waiting = 0) and (mem0 != nobody))
        [
          launch-faf mem0
        ]
        [
          if (mem0 != nobody)
          [
            launch-rocket towards mem0
          ]
        ]
        set mem5 0 
      ]
    ]
  ]
end

to durandil-drive-harvesters
  let food one-of perceive-food
  if (food != nobody) [
    durandil-call-harvester [xcor] of food [ycor] of food 
  ]
end

to durandil-safe-move
  let done false
  let c 0
  while ([done = false and c < 360])
  [
    ifelse (free-ahead? speed = nobody) 
    [
      forward-move speed
      set done true
    ]
    [
      rt 20 - random 10
      set c c + 10
    ]
  ]
end

to durandil-init-target-explorer
  let x 0
  let y 0
  if ([xcor] of one-of my-bases = 0)
  [
    ifelse (random 2 = 0)
    [set x -40]
    [set x 40]
    ifelse (random 2 = 0)
    [set y -10]
    [set y 10]
  ]
  set mem0 [0 0]
  set mem0 replace-item 0 mem0 x 
  set mem0 replace-item 1 mem0 y
end

; the explorer go turn around the ennemy base
to durandil-go-say-hello
  set mem5 1
  set mem2 [0 0]
  if (Breed = RocketLaunchers)
  [
    let c int (ticks / 10)
    ;set mem2 replace-item 0 mem2 3
    ;set mem2 replace-item 0 mem2 5
    set mem2 replace-item 0 mem2 (2 + c mod 6)
    set mem2 replace-item 1 mem2 (4 + c mod 6)
  ]
  if (Breed = Explorers)
  [
    set mem2 replace-item 0 mem2 5
    set mem2 replace-item 1 mem2 10
  ]
  
  ; decide to go back if base destroyed
  if (distancexy item 0 mem0 item 1 mem0 < 5)
  [
    let b one-of perceive-base ennemy1
    if (b = nobody) [ set b one-of perceive-base ennemy2 ]
    if (b = nobody) [ durandil-init-target-explorer ]
  ]
  
  ; shooting
  if ((distancexy item 0 mem0 item 1 mem0 <= 10) and (Breed = RocketLaunchers))
  [ durandil-shoot ]
  
  if (distancexy item 0 mem0 item 1 mem0 > item 1 mem2)
  [
    set heading towardsxy item 0 mem0 item 1 mem0
    rt 20 - random 20
    durandil-safe-move
  ]
  if (distancexy item 0 mem0 item 1 mem0 <= item 0 mem2)
  [
    set heading towardsxy item 0 mem0 item 1 mem0
    rt 135
    rt 40 - random 80
    durandil-safe-move
  ]
  if (distancexy item 0 mem0 item 1 mem0 > item 0 mem2 and distancexy item 0 mem0 item 1 mem0 <= item 1 mem2)
  [
    set heading towardsxy item 0 mem0 item 1 mem0
    rt 90
    rt 20 - random 20
    durandil-safe-move
  ]
end

to goRedExplorer
  ;; si la base donne l'ordre de se désencombrer
  ifelse (mem3 > 0 ) [
    random-move
    set mem3 ( mem3 - 1 )
  ]
  [
    ifelse (energy < 500)
    [ durandil-go-back-to-base-without-food ]
    [durandil-go-and-eat ]
  
    durandil-drive-harvesters

    ifelse (not durandil-no-target?) [
      durandil-call-rocket-launcher-xy item 0 mem0 item 1 mem0
      durandil-call-explorer item 0 mem0 item 1 mem0
    ]
    [ let h one-of perceive-robots2 ennemy1 ennemy2
      if ( h != nobody ) [ durandil-call-rocket-launcher-t h]
    ]
  ]
end

to goRedRocketLauncher
  ;; si la base donne l'ordre de se désencombrer
  ifelse (mem3 > 0 ) [
    random-move
    set mem3 ( mem3 - 1 )
  ]
  [
    ifelse (energy < 500) or (nb-missiles = 0)
    [
      ;; rentre à la base si certaines conditions sont vérifiées
      durandil-go-back-to-base-without-food
    ]
    [
      ifelse(mem4 = 2)
      [
        durandil-go-say-hello
      ]
      [
        ;; sinon sélectionne une cible
        durandil-select-target
        ifelse (durandil-no-target?)
        ;; si pas de cible, cherche à manger
        [ durandil-go-and-eat ]
        ;; sinon tire
        [ 
          durandil-shoot 
          ;let dummy durandil-dodge
        ]
      ]
    ]
  ]
  ;; crée un nouveau missile si plus de 3000 unités d'énergie
  if ((energy > 1000) and (nb-missiles < 5)) [ new-missile 1 ]
end

to goRedHarvester
  ;; si la base donne l'ordre de se désencombrer
  ifelse (mem3 > 0 ) [
    random-move
    set mem3 ( mem3 - 1 )
  ]
  [
  ;; le harvester doit il back ? 
  if (mem4 != 1 and ( (carrying-food? > 500) or (energy < 500) ) )[
   ; retour à la base
    set mem4 1
    ;; détermine la base la plus proche
    set mem1 min-one-of my-bases [ distance myself ]
    ;; si l'ID mod 7 du harvester est 0, il rentre à la base la plus eloignée afin d'équilibrer un peu
    if (Breed = Harvesters and who mod 7 = 0) [ 
      set mem1 max-one-of my-bases [ distance myself ] 
    ]
  ]
  ifelse (mem4 = 1) 
  [
   ;; si la base de back a été détruite
  if ( mem1 = nobody ) [ 
    ;; détermine la base la plus proche
    set mem1 min-one-of my-bases [ distance myself ]
    if (Breed = Harvesters and who mod 7 = 0) [ 
      set mem1 max-one-of my-bases [ distance myself ] 
   ] 
  ]
    ;; si il est assez proche de la base, l'aléatoire augmente pour planter plus
    if (distance mem1 < 10) and (carrying-food? > 300) [ 
      durandil-plant-seeds
    ]
    
    ;; rentre à la base si certaines conditions sont vérifiées
    durandil-harvester-go-back-to-base
    if (energy > 500) and (carrying-food? > 500) [ 
      ;; détermine la distance de la base la plus proche
      if (min [ distance myself ] of my-bases < 10) [
        plant-seeds color max-seeds
      ]
    ]
  ]
  [
    ;; cherche à manger
    durandil-harvesters-go-and-eat
  ]
  ]
end

to goRedBase
  set label energy
  if (nb-missiles < 10) and (energy > 1000) [ new-missile 50 ]
  if (energy > 12000) [ 
    if (mem1 = 1)
    [
      new-RocketLauncher 1 self
    ]
    let r random 6
    ifelse (r = 0) 
    [new-Explorer 1 self]
    [
      ifelse (r <= 3) 
      [ new-Harvester 1 self ]
      [ new-RocketLauncher 1 self ]
    ]
  ]
  let t one-of perceive-specific-robots2 ennemy1 ennemy2 rocketlaunchers
  if (t != nobody) [
    if (random 10 = 0) [launch-rocket towards t]
  ]
  let nb count perceive-robots friend
  if (nb > 20)
  [
    if (mem1 = 0)[print ("trop d'unité")]
    set mem1 1
  ]
  if (nb < 10)
  [
    if (mem1 = 1)[print ("pas assez d'unité")]
    set mem1 0
  ]
end

;; procedure pour initialiser les explorers verts
to initRedExplorer
  set mem0 [0 0]
  ifelse (random 4 = 0)
  [
    set mem4 2 
    durandil-init-target-explorer
  ]
  [set mem4 0]
end

;; procedure pour initialiser les rocket-launchers verts
to initRedRocketLauncher
  set mem0 [0 0]
  ifelse (random 1 = 0)
  [
    set mem4 2 
    set mem5 1
    durandil-init-target-explorer
  ]
  [set mem4 0]
end

;; procedure pour initialiser les harvesters verts
to initRedHarvester
  set mem0 [0 0]
end

;; procedure pour initialiser les bases verts
to initRedBase
  set mem0 [0 0]
  ; cré des explorers, des harvesters et des rocket-launchers
  new-Explorer 2 self
  new-Harvester 5 self
  new-RocketLauncher 2 self
end
